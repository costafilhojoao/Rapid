% RBC com governo



%--------------------------------------------------------------------------------------------------------------------------------------
% 1. Definição das Variáveis
%--------------------------------------------------------------------------------------------------------------------------------------

var K I Y A L W C R G;

varexo ea eg;

%--------------------------------------------------------------------------------------------------------------------------------------
% 2. Calibração
%--------------------------------------------------------------------------------------------------------------------------------------

parameters beta psi delta alpha rhoa rhog GoY;

alpha   = 0.33;         
beta    = 0.99;         
psi     = 2.407769275590489;     
delta   = 0.025;        
rhoa    = 0.95;         
rhog    = 0.98;
GoY     = 0.2;

%--------------------------------------------------------------------------------------------------------------------------------------
% 3. Modelo
%--------------------------------------------------------------------------------------------------------------------------------------

model;

%%%%%%%%%%%%% Famílias %%%%%%%%%%%%% 

[name = 'Lei de Movimento do Capital']
K = ( 1 - delta ) * K(-1) + I;

[name = 'Equação de Euler']
1 / C = beta * ( R(+1) + 1 - delta ) / C(+1);

[name = 'Oferta de Trabalho']
W = psi * C / ( 1 - L );


%%%%%%%%%%%%% Empresas %%%%%%%%%%%%% 

[name = 'Função de Produção']
Y = A * ( K(-1)^alpha ) * ( L^( 1 - alpha ) );

[name = 'Demanda por Trabalho']
W = ( 1 - alpha )  * A * ( K / L ) ^ alpha;

[name = 'Demanda por Capital']
R = alpha  * A * ( K / L ) ^ ( alpha - 1 );

[name = 'Produtividade']
log( A ) = ( 1 - rhoa ) * log ( steady_state( A ) )  + rhoa * log( A(-1) ) + ea;

%%%%%%%%%%%%% Governo %%%%%%%%%%%%%

[name = 'Gastos do governo']
log( G ) = ( 1 - rhog ) * log ( steady_state( G ) )  + rhog * log( G(-1) ) + eg;

%%%%%%%%%%%%% Agragação %%%%%%%%%%%%% 

[name = 'Estrição macroeconômica']
Y = C + I + G;


end;

%--------------------------------------------------------------------------------------------------------------------------------------
% 4. Equilíbrio
%--------------------------------------------------------------------------------------------------------------------------------------

steady_state_model;

A = 1;
L = 0.33;
R = 1 / beta - 1 + delta;
W = (1 - alpha ) * ( R / alpha ) ^( alpha / ( alpha - 1 ) );
Y = L * ( R / alpha ) ^( alpha / ( alpha - 1 ) ) ;
K = L * ( Y / L ) ^( 1 / alpha );
I = delta * K;
G = GoY * Y;
C = Y - I - G;
end;

steady;
check;
model_diagnostics;
model_info;

%--------------------------------------------------------------------------------------------------------------------------------------
% 5. Simulação
%--------------------------------------------------------------------------------------------------------------------------------------

shocks;

var ea;
stderr 0.007;
var eg;
stderr 0.0005;
end;

stoch_simul(ar=1, order=1, irf=100);